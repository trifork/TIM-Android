plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.plugin.serialization'
}

apply from: '../dependencies.gradle'

android {
    compileSdk 33


    defaultConfig {
        // The minSdk should never be lower than 24, as it relies on the keystore to be properly working.
        // See https://developer.android.com/jetpack/androidx/releases/security#security-crypto-1.1.0-alpha01
        // and https://developer.android.com/training/articles/keystore#SupportedCiphers
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        //TODO(Necessary for test to run. But causes the sdk to catch redirects. Move this to testOptions? or change to some invalid value?)
        android.defaultConfig.manifestPlaceholders = ['appAuthRedirectScheme': 'test']
    }

    buildTypes {
        development {
            applicationIdSuffix ".debugDevelopment"
            initWith debug
            debuggable true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {

    //region Android
    implementation androidX.coreKtx
    implementation androidX.appCompat
    //endregion

    //region Kotlin
    implementation kotlin.coroutinesAndroid.coroutinesAndroid
    testImplementation kotlin.coroutinesAndroid.coroutinesAndroidTest
    implementation kotlin.serialization.json
    //endregion

    //region Testing
    testImplementation jUnit.junit
    testImplementation roboleltric.roboleltric

    androidTestImplementation mockk.android
    androidTestImplementation androidX.test.androidJUnit
    androidTestImplementation androidX.test.espresso
    //endregion

    //region TIM
    releaseApi timEncryptedStorage
    debugApi timEncryptedStorage
    developmentApi project(':TIMEncryptedStorage-Android')
    //endregion

    //region AppAuth
    implementation appAuth
    //endregion

    //region Biometric
    implementation biometric.biometric
    //endregion

    //region Lifecycle
    implementation lifecycle.commonJava8
    implementation lifecycle.androidx
    //endregion
}

apply from: '../publish.gradle'